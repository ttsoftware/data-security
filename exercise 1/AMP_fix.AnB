Protocol: AMP

Types: Agent C,s,RP;
       Number Request,ReqID,Data;
       PublicKey K

Knowledge:
       C: C,s,RP,pk(s),pk(C),inv(pk(C)),pk(RP);
       s: C,s,pk(s),inv(pk(s)),pk(C);
       RP: s,RP,pk(s),pk(RP),inv(pk(RP))
       where RP!=s

Actions:

# We sign with C's private key, since RP does not know C's public key
C->RP: {C,RP,{Request}inv(pk(C)),K}pk(RP)
RP->C: {C,s,RP,ReqID,{Request}inv(pk(C))}K

# We give s RP's public key, such that s can encrypt its response, such that only RP can read it.
C->s:  { {C,s,pk(RP),ReqID,Request}inv(pk(C)) }pk(s)
s->C:  { {C,s,Request}inv(pk(s)) }pk(RP)

# Now Request is gauranteed to be unique for the whole request chain.
C->RP: {{C,s,Request}inv(pk(s))}pk(RP)
RP->C: {Data}K

Goals:

RP authenticates C on Request
C authenticates RP on Data
Data secret between RP,C
